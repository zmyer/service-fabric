# -------------------------------------------------------------------------------------------------
# Test - DeleteServiceWithPartitionCountMismatch                                   [owner] brcamina
# -------------------------------------------------------------------------------------------------
# RDBUG:11775800 -> This test places a single replica stateful persisted service with 3 partitions
# on a single node. One of the replicas is moved to another node and the first node is taken down
# as FM rebuild is triggered. FM rebuild will only see 1 FT while the partition count will be
# 3. This test then verifies that calling deleteservice times out because of the partition
# count and FT size mismatch. ServiceDelete is called again with isForce=true and it completes
# with success.
# -------------------------------------------------------------------------------------------------

votes 10 20 30
fmservice 1 1
cmservice 3 1
namingservice 1 3 3
cleantest

set DummyPLBEnabled true
set FMPlacementConstraints FM==true

+10 nodeprops=FM:false
+20 nodeprops=FM:true
+30 nodeprops=FM:true
verify

# An application is used because RA tries to terminate the application process if isForce is used
# so this test can't use AdHoc services.
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA1 stateful persist
app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA1 version=1.0
app.upload version10
provisionapp version10

createapp fabric:/app TestApp 1.0
verify

createservice fabric:/app/test TestApp_App0:ServicePackageA:ServiceTypeA1 y 3 1 persist appname=fabric:/app
verify

swapprimary fabric:/app/test 30 20
!waitforstate FM.Replica.Role.fabric:/app/test.20 Primary

# Trigger rebuild and wait for it to complete
-30 removedata
nodestateremoved 30
!waitforstate FM.IsReady true

# Delete without force should timeout and the service should not be deleted
deleteservice fabric:/app/test errors=Timeout
!state FM.Service.IsDeleted.fabric:/app/test false

# This is an async operation, so we need a timeout that's large enough for CM to finish working on it
set NamingOperationTimeout 360
# deleteservice with isForce should skip partition count verification
deleteservice fabric:/app/test isForce
verify

!waitforstate FM.Service.IsDeleted.fabric:/app/test true

!q