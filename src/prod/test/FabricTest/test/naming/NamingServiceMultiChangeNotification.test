set DummyPLBEnabled true
votes 10 20 30
cmservice 0 0
namingservice 1 3 2
cleantest
set NamingOperationTimeout 25
set NamingOperationRetryTimeout 60
set NamingServiceChangePollInterval 25

+10
+20
+30
verify
+40
+50
+60
+70
+80
+90
verify

createclient c1 nodeconfig=40
!pause,2

#
# Register multiple callbacks using the same client
# Depending on the state of the system, check that the right callbacks are being called
# with either new resolved service partition or failure
#

# Register callback for singleton partition 
# Callbacks for (Test1, PK None) -> cbNone1
#
registercallback c1 cbNone1 fabric:/test1 false 

# Name doesn't exist: wait for failure notifications
#
waitforcallback c1 cbNone1 error=UserServiceNotFound

createname fabric:/test1
verify

# Register callback for Int64 and Named partition 
# Callbacks for (Test1, PK 30) -> cbInt1
# Callbacks for (Test1, PK Random) -> cbNamed1
#
registercallback c1 cbInt1 fabric:/test1 30 
registercallback c1 cbNamed1 fabric:/test1 Random1

# Service doesn't exist: wait for failure notifications
#
waitforcallback c1 cbInt1 error=UserServiceNotFound

waitforcallback c1 cbNamed1 error=UserServiceNotFound

#
# Add a persisted service with 1 partition. 
# Wait for: Address Int, Failure for None and Named
#
createservice fabric:/test1 TestPersistedStoreServiceType y 1 4 persist
verify

# Register for a callback for a invalid int64 partition key
registercallback c1 cbIntInvalid1 fabric:/test1 300000 

# Callbacks for (Test1, PK 30) -> cbInt1,cbInt2
registercallback c1 cbInt2 fabric:/test1 30 

waitforcallback c1 cbInt1
waitforcallback c1 cbNone1 error=InvalidServicePartition
waitforcallback c1 cbNamed1 error=InvalidServicePartition
waitforcallback c1 cbInt2
waitforcallback c1 cbIntInvalid1 error=InvalidServicePartition

unregistercallback c1 cbIntInvalid1 

#
# Trigger a change in the system
# Wait for: int callbacks should raise address 
# 
verify
swapprimary fabric:/test1 90 70 force
waitforcallback c1 cbInt1
waitforcallback c1 cbInt2

#
# Add another callback; it should receive the previous notification
# Callbacks for (Test1, PK 30) -> cbInt1,cbInt2,cbInt3
#
registercallback c1 cbInt3 fabric:/test1 30
waitforcallback c1 cbInt3

#
# Register a callback for a different key
# owned by the same partition.
# Callbacks for (Test1, PK 32) -> cbInt4
#
registercallback c1 cbInt4 fabric:/test1 32
waitforcallback c1 cbInt4

#
# Trigger a change in the system. All Int handlers should raise address
#
verify
swapprimary fabric:/test1 70 80 force
waitforcallback c1 cbInt1
waitforcallback c1 cbInt2
waitforcallback c1 cbInt3
waitforcallback c1 cbInt4

verify 

#
# Unregister one Int handler and trigger a change in the system.
# The other Int handlers should be notified
#
# Callbacks for (Test1, PK 30) -> cbInt1,cbInt3
# Callbacks for (Test1, PK 32) -> cbInt4
unregistercallback c1 cbInt2

verify
swapprimary fabric:/test1 80 90 force
waitforcallback c1 cbInt1
waitforcallback c1 cbInt3
waitforcallback c1 cbInt4

# 
# Delete service - all handlers should report failure
#
deleteservice fabric:/test1 

waitforcallback c1 cbNone1 error=UserServiceNotFound
waitforcallback c1 cbNamed1 error=UserServiceNotFound
waitforcallback c1 cbInt1 error=UserServiceNotFound
waitforcallback c1 cbInt3 error=UserServiceNotFound
waitforcallback c1 cbInt4 error=UserServiceNotFound

verify

#
# Create service with the same partition
# Int handlers should notify new address
#
createservice fabric:/test1 TestPersistedStoreServiceType y 1 4 persist
verify

waitforcallback c1 cbInt1
waitforcallback c1 cbInt3
waitforcallback c1 cbInt4
waitforcallback c1 cbNone1 error=InvalidServicePartition
waitforcallback c1 cbNamed1 error=InvalidServicePartition

# 
# Delete service and create it with None partition key
# Int handlers should raise failure
# None handlers should raise address
#
deleteservice fabric:/test1 
verify

waitforcallback c1 cbNone1 error=UserServiceNotFound
waitforcallback c1 cbInt1 error=UserServiceNotFound
waitforcallback c1 cbNamed1 error=UserServiceNotFound

createservice fabric:/test1 CalculatorServiceType n 0 1
verify

waitforcallback c1 cbNone1
waitforcallback c1 cbInt1 error=InvalidServicePartition
waitforcallback c1 cbNamed1 error=InvalidServicePartition

unregistercallback c1 cbInt1
unregistercallback c1 cbInt3
unregistercallback c1 cbInt4

# Restart the gateway to clear its cache, and trigger a change in the system while it's down;
# the registered callback should receive the update after the gateway comes online
# When the gateway is down, the handlers should be notified that gateway is unreachable
-40

waitforcallback c1 cbNone1 error=CommunicationError

# Kill the node where the stateless service is placed, add the gateway and
# create another service to potentially trigger a broadcast for a partial range
-90
+40
createservice fabric:/test2 TestPersistedStoreServiceType y 2 1 persist partitionnames=Random1,Random2
verify

waitforcallback c1 cbNone1

# Register for 2 valid and non-valid named partition
# Callbacks for (Test2, PK Random1) -> cbNamed2
# Callbacks for (Test2, PK Random2) -> cbNamed3
# Callbacks for (Test2, PK OtherRandom) -> cbNamed4
#
registercallback c1 cbNamed2 fabric:/test2 OtherRandom
registercallback c1 cbNamed3 fabric:/test2 Random1
registercallback c1 cbNamed4 fabric:/test2 Random2

waitforcallback c1 cbNamed2 error=InvalidServicePartition
waitforcallback c1 cbNamed3
waitforcallback c1 cbNamed4

#
# Delete named service and re-create with different named partitions:
# one of the old names is removed and a new one is added.
# The one removed should receive InvalidServicePartition, the other one the address

deleteservice fabric:/test2
verify

waitforcallback c1 cbNamed2 error=UserServiceNotFound
waitforcallback c1 cbNamed3 error=UserServiceNotFound
waitforcallback c1 cbNamed4 error=UserServiceNotFound

createservice fabric:/test2 TestPersistedStoreServiceType y 2 1 persist partitionnames=OtherRandom,Random2
verify

waitforcallback c1 cbNamed2 
waitforcallback c1 cbNamed3 error=InvalidServicePartition
waitforcallback c1 cbNamed4

unregistercallback c1 cbNone1
unregistercallback c1 cbNamed1 
unregistercallback c1 cbNamed2 
unregistercallback c1 cbNamed3 
unregistercallback c1 cbNamed4 

deleteclient c1

!q