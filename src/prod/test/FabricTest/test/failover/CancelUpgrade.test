#
# [owner] aprameyr
# Verify cancel upgrade scenarios
# Ensure that in case a rollback happens and FM sends CancelUpgrade message to RA
# RA does not send CancelUPgradeReply until it has told FM of all replica that are going down
# 
set DummyPLBEnabled true
set NamingOperationTimeout 10
set RAPMessageRetryInterval 1

votes 10 20 30
cmservice 3 1
fmservice 3 1
namingservice 1 3 1
cleantest

+10 nodeprops=StringProperty:SeedNode ud=ud6 version=5.0.0.0:v1
+20 nodeprops=StringProperty:SeedNode ud=ud5 version=5.0.0.0:v1
+30 nodeprops=StringProperty:SeedNode ud=ud4 version=5.0.0.0:v1

verify

+40 nodeprops=StringProperty:NormalNode ud=ud1 version=5.0.0.0:v1
+50 nodeprops=StringProperty:NormalNode ud=ud2 version=5.0.0.0:v1
+60 nodeprops=StringProperty:NormalNode ud=ud3 version=5.0.0.0:v1
verify

prepareupgradefabric code=5.0.0.0 config=v1
prepareupgradefabric code=6.0.0.0 config=v2

provisionfabric code=5.0.0.0 config=v1
provisionfabric code=6.0.0.0 config=v2

#
# Create app
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA1 stateful persist
app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA1 version=1.0
app.upload version10
provisionapp version10

app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackageA version=1.1
app.servicetypes version11 ServicePackageA ServiceTypeA1 stateful persist
app.codepack version11 ServicePackageA CodeA1 types=ServiceTypeA1 version=1.1
app.upload version11
provisionapp version11

#
# Start app
#
createapp fabric:/app TestApp 1.0
verify

#
# Create a service in the app
#
createservice fabric:/app/test TestApp_App0:ServicePackageA:ServiceTypeA1 y 1 3 persist constraint=(StringProperty!=SeedNode) appname=fabric:/app
verify

upgradeapp fabric:/app 1.1 Rolling upgrademode=manual restart
verifyupgradeapp fabric:/app upgradedomains=ud1

# Block ReplicaUp so that the replica stays down on RA and Up on FM
# Upgrade is stuck on RA
addbehavior b1 60 * ReplicaUp
addbehavior b2 60 * StatefulServiceReopen

# Move to UD2 - this should complete and FM should wait at UD3
upgradeappmovenext fabric:/app
verifyupgradeapp fabric:/app upgradedomains=ud1,ud2

# Move to UD3
# This should get stuck - the node should be reopening the replicas
# The FM is waiting because the upgrade reply from 30 is pending
upgradeappmovenext fabric:/app
!waitforstate RA.Replica.IsUp.fabric:/app/test.60.60 false

# let timeout be allowed because the CM stops 
upgradeapp fabric:/app 1.0 Rolling upgrademode=manual restart errors=Success,ApplicationUpgradeInProgress,Timeout

!pause 20

# Expect that the rollback is stuck because there is no Cancel Reply from the node
# This is because on the node ReplicaUp is blocked which causes Cancel to be ignored 
# Because Cancel can only be processed and replied to once FM knows about all down replicas
verifyupgradeapp fabric:/app timeout=30 expectTimeout upgradestate=rollbackinprogress

# Verify above assertions about the state
!waitforstate FM.Replica.IsUp.fabric:/app/test.60 true

# RA state should be changed to Down and FM above should verify that it is up
!waitforstate RA.Replica.IsUp.fabric:/app/test.60.60 false

# Unblock ReplicaUp and block reply
# In the current implementation this should cause the upgrade to go ahead 
# Which is incorrect as on node 60 the replica is still down
# This should cause the upgrade to still be stuck on ud3 because of the replica check timeout as the replica on 60 is down
addbehavior bx * 60 ReplicaUpReply
removebehavior b1
verifyupgradeapp fabric:/app timeout=30 expectTimeout upgradestate=rollbackinprogress

# At this time the replica states should be consistent
!waitforstate FM.Replica.IsUp.fabric:/app/test.60 false
!waitforstate RA.Replica.IsUp.fabric:/app/test.60.60 false

#Unblock Reopen and replica down reply and let the replica come up
#This should cause replicasetcheck to proceed
removebehavior bx
removebehavior b2

!waitforstate FM.Replica.IsUp.fabric:/app/test.60 true
!waitforstate RA.Replica.IsUp.fabric:/app/test.60.60 true

#Now the upgrade should progress past ud1
verifyupgradeapp fabric:/app
# upgradestate=rollbackcompleted


#Repeat the same scenario, but for fabric upgrade


# Perform baseline upgrade for v1
upgradefabric code=5.0.0.0 config=v1 Rolling
verifyupgradefabric timeout=300

# Perform upgrade v1 -> v2
upgradefabric code=6.0.0.0 config=v2 Rolling upgrademode=manual

verifyupgradefabric upgradedomains=ud1

upgradefabricmovenext
verifyupgradefabric upgradedomains=ud1,ud2

# NodeFabricUpgradeRequest is blocked to node 60. Upgrade should get stuck here
addbehavior b1 * 60 NodeFabricUpgradeRequest
upgradefabricmovenext

# wait for FM to mark the node as UpgradePending. Upgrade won't proceed because
# the upgrade message is still blocked for that node
!waitforstate FM.Node.IsPendingFabricUpgrade.60 true

rollbackfabric
setrollbackfabric code=5.0.0.0 config=v1 failurereason=Interrupted

# Wait for FM to receive the rollback message
!waitforstate FM.FabricUpgradeTargetVersion 5.0.0.0:v1

# Remove the upgrade message to the node. Rollback should be unstuck and complete
# FM Will send CancelUpgradeRequest to node 60. Node will reply with CancelUpgradeReply
# FM should process this message and proceed with the rollback
removebehavior b1

verifyupgradefabric

!q