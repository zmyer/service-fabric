set DummyPLBEnabled true
votes 10
cleantest
fmservice 3 1
cmservice 0 0
namingservice 1 3 1

set ExpectedClusterSize 5
set NamingOperationTimeout 15
set NamingOperationRetryTimeout 180

+10
+20
+30
+40
+50
verify

# Verify the location of FMService replicas
!waitforstate FM.Replica.Role.FMService.50 Primary
!waitforstate FM.Replica.Role.FMService.40 Secondary
!waitforstate FM.Replica.Role.FMService.30 Secondary

# Verify the location of NamingService replicas
!waitforstate FM.Replica.Role.NamingService.50 Primary
!waitforstate FM.Replica.Role.NamingService.40 Secondary
!waitforstate FM.Replica.Role.NamingService.30 Secondary

# Create a persisted stateful service
createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist
verify

# Verify the location of user service replicas
!waitforstate FM.Replica.Role.fabric:/persisted.50 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.40 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.30 Secondary

# Attemp to recover a partition that does not exist
recoverpartition 11111111-1111-1111-1111-111111111111 PartitionNotFound

# Capture the PartitionIDs of all services
!var FMServiceFTID FM.FT.PartitionId.FMService
!var NamingServiceFTID FM.FT.PartitionId.NamingService
!var UserServiceFTID FM.FT.PartitionId.fabric:/persisted

# Bring the FMService into quorum loss
addbehavior b1 * * GetLSN
-50
-40
!waitforstate FM.FT.QuorumLost.FMService true
removebehavior b1

# Recover the FMService. This will cause data loss and trigger full rebuild.
recoverpartition <var.FMServiceFTID>

# Verify that the FMService is no longer in quorum loss
!waitforstate FM.FT.QuorumLost.FMService false

# Verify that the NamingService and the user service are in quorum loss state
!waitforstate FM.FT.QuorumLost.NamingService true
!waitforstate FM.FT.QuorumLost.fabric:/persisted true

# Recover the NamingService and the user service
recoverpartition <var.NamingServiceFTID>
recoverpartition <var.UserServiceFTID>

# Verify that the NamingService and the user service are no longer in quorum loss state
!waitforstate FM.FT.QuorumLost.NamingService false
!waitforstate FM.FT.QuorumLost.fabric:/persisted false

verify

# Verify the location of FMService replicas
!waitforstate FM.Replica.Role.FMService.30 Primary
!waitforstate FM.Replica.Role.FMService.20 Secondary
!waitforstate FM.Replica.Role.FMService.10 Secondary

# Verify the location of NamingService replicas
!waitforstate FM.Replica.Role.NamingService.30 Primary
!waitforstate FM.Replica.Role.NamingService.20 Secondary
!waitforstate FM.Replica.Role.NamingService.10 Secondary

# Verify the location of user service replicas
!waitforstate FM.Replica.Role.fabric:/persisted.30 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.20 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.10 Secondary

addbehavior b1 * * ChangeNotification
-30
-20
-10
removebehavior b1

!pause 1
clearticket

set ExpectedClusterSize 1

+10
!wait
!waitforstate FMM.Node.10 true

# The FMService should be in quorum loss. Recover it.
recoverpartition <var.FMServiceFTID>

# Verify that the FMService is no longer in quorum loss
!waitforstate FM.FT.QuorumLost.FMService false

!waitforstate FM.Node.IsUp.10 true
ProcessPendingPlbUpdates

# The NamingService and the user service should be in quorum loss. Recover these services.
recoverpartition <var.NamingServiceFTID>
recoverpartition <var.UserServiceFTID>

# Verify that the NamingService and the user service are no longer in quorum loss state
!waitforstate FM.FT.QuorumLost.NamingService false
!waitforstate FM.FT.QuorumLost.fabric:/persisted false

+20
+30
verify

# Bring the user service into quorum loss
addbehavior b1 * * DoReconfiguration
removeruntime 20 y
removeruntime 30 y
!waitforstate FM.FT.QuorumLost.fabric:/persisted true
removebehavior b1

# Attempt to delete the user service. It should timeout becasue the service is in quorum loss.
deleteservice fabric:/persisted errors=Timeout

# Recover the user service
recoverpartition <var.UserServiceFTID>

# The user service should now be deleted
deleteservice fabric:/persisted errors=ServiceNotFound

addruntime 20 y
addruntime 30 y

verify

-*

!q
