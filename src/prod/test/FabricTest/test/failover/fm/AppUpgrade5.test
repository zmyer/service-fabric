#
# [owner] MMohsin, AnuragG
#
# This test covers the scenario where a volatile service has a TargetReplicaSetSize of 1.
# During application upgrade, the primary should be proactively moved out of the node,
# and moved back in after the upgrade is complete on that node.
#

votes 10 20 30
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set DummyPLBEnabled true
set UpgradeStatusPollInterval 5
set ExpectedReplicaUpgradeDuration 3600
set IsSingletonReplicaMoveAllowedDuringUpgrade true

+10 ud=UD3
+20 ud=UD2
+30 ud=UD1
verify

#
# Application version 1.0
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA VolatileServiceType stateful
app.servicetypes version10 ServicePackageA PersistedServiceType stateful persist
app.codepack version10 ServicePackageA CodeA1 types=VolatileServiceType,PersistedServiceType version=1.0
app.upload version10
provisionapp version10

#
# Application version 1.1
#
app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackageA version=1.1
app.servicetypes version11 ServicePackageA VolatileServiceType stateful
app.servicetypes version11 ServicePackageA PersistedServiceType stateful persist
app.codepack version11 ServicePackageA CodeA1 types=VolatileServiceType,PersistedServiceType version=1.1
app.upload version11
provisionapp version11

# Create application version 1.0
createapp fabric:/MyApp TestApp 1.0
verify

# Create services
createservice fabric:/MyApp/Volatile VolatileServiceType y 1 1 appname=fabric:/MyApp
createservice fabric:/MyApp/Persisted PersistedServiceType y 1 1 persist appname=fabric:/MyApp
verify

!waitforstate FM.Replica.Role.fabric:/MyApp/Volatile.30 Primary
!waitforstate FM.Replica.Role.fabric:/MyApp/Persisted.30 Primary

# Block the NodeUpgradeRequest so that node 30 does not start the upgrade
addbehavior b1 * * NodeUpgradeRequest

#
# Perform application upgrade from version 1.0 to 1.1
#
upgradeapp fabric:/MyApp 1.1 Rolling

# The primary replica should be moved out of node 30
!waitforstate FM.Replica.Role.fabric:/MyApp/Volatile.20 Primary
!waitforstate FM.Replica.Role.fabric:/MyApp/Persisted.20 Primary

# Allow the upgrade to proceed
removebehavior b1

verifyupgradeapp fabric:/MyApp

# The primary replica should have been moved back to node 30
!waitforstate FM.Replica.Role.fabric:/MyApp/Volatile.30 Primary
!waitforstate FM.Replica.Role.fabric:/MyApp/Persisted.30 Primary

verify

-*

!q
