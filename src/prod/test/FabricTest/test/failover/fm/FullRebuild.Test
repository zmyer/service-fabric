set DummyPLBEnabled true
votes 40 50 60
cmservice 0 0
namingservice 1 7 1
set ExpectedClusterSize 3
set FullRebuildWaitDuration 1
set GracefulReplicaShutdownMaxDuration 0

fmservice 5 1
cleantest

+30
+40
+50
verify


+20
+60
verify

+70
+80
verify

!waitforstate FM.Replica.Role.FMService.50 Primary
!waitforstate FM.Replica.Role.FMService.20 Secondary
!waitforstate FM.Replica.Role.FMService.30 Secondary
!waitforstate FM.Replica.Role.FMService.40 Secondary
!waitforstate FM.Replica.Role.FMService.60 Secondary

# Create a stateless service
createname fabric:/stateless
createservice fabric:/stateless CalculatorServiceType n 2 4

# Create a volatile stateful service
createname fabric:/volatile
createservice fabric:/volatile TestStoreServiceType y 1 5

# Create a persisted stateful service
createname fabric:/persisted
createservice fabric:/persisted TestPersistedStoreServiceType y 1 7 persist
verify

# Disable NodeDown notifications to FM{M}
addbehavior b1 * * ChangeNotification

# Remove nodes 50, 30, and 20.
# This will cause FMM rebuild; and due to not enough reports,
# the FM Service FailoverUnit will get get stuck while in-build.
# After FullRebuildWaitDuration, this will trigger full rebuild.
-50
-30
-20

# Start the full rebuild
removebehavior b1

verify

# Resolve service locations
resolve fabric:/stateless 100
resolve fabric:/volatile 100
resolve fabric:/persisted 100

# Add the nodes back
+20
verify
+30
+50
verify

!waitforstate FM.Replica.Role.FMService.80 Primary
!waitforstate FM.Replica.Role.FMService.20 Secondary
!waitforstate FM.Replica.Role.FMService.40 Secondary
!waitforstate FM.Replica.Role.FMService.60 Secondary
!waitforstate FM.Replica.Role.FMService.70 Secondary

# Do not let the reconfiguration to complete
addbehavior b2 * * GetLSN

# Remove nodes 40, 60, and 80.
# This will result in quorum loss for the FM Service,
# and will trigger full rebuild after FullRebuildWaitDuration.
-80
-60
-40

# Wait for quorum loss
!waitforstate FM.FT.QuorumLost.FMService true

# Start the full rebuild
removebehavior b2

verify

# Resolve service locations
resolve fabric:/stateless 100
resolve fabric:/volatile 100
resolve fabric:/persisted 100

# Add the nodes back
+40
verify
+60
+80
verify

!waitforstate FM.Replica.Role.FMService.70 Primary
!waitforstate FM.Replica.Role.FMService.20 Secondary
!waitforstate FM.Replica.Role.FMService.30 Secondary
!waitforstate FM.Replica.Role.FMService.40 Secondary
!waitforstate FM.Replica.Role.FMService.50 Secondary

# Set FullRebuildWaitDuration to large value to disable full rebuild on quorum loss.
set FullRebuildWaitDuration 9999

# Do not let the reconfiguration to complete
addbehavior b2 * * GetLSN

# Remove nodes 30, 40, and 70.
# This will result in quorum loss for the FM Service.
-70
-40
-30

# Wait for quorum loss
!waitforstate FM.FT.QuorumLost.FMService true

# Detect quorum loss
removebehavior b2

# Call RecoverPartitions. This will immediately trigger full rebuild.
recoverpartitions

verify

# Resolve service locations
resolve fabric:/stateless 100
resolve fabric:/volatile 100
resolve fabric:/persisted 100

# Delete services
deleteservice fabric:/stateless
deleteservice fabric:/volatile
deleteservice fabric:/persisted

verify

# Remove nodes
-*

!q
