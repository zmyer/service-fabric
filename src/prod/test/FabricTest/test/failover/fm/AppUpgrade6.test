#
# [owner] MMohsin
# [bug] 5069292
#
# This test case covers the scenario where the following sequence of events happen:
#
# 1. A volatile replica is getting upgraded.
# 2. The replica goes down (dropped) due to upgrade (the replica location is marked as preferred).
# 3. After the node has been upgraded, a new replica is being placed at the preferred location.
# 4. Before the new replica is placed successfully, the replica goes down (dropped).
# 5. Upgrade moves to the next UD.
#
# In this state, we should still try to place a new replica at the preferred location. However, due to the above bug,
# the node is not considered upgraded and hence no attempt is made to create a replacement replica.
#

votes 10 20 30
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set DummyPLBEnabled true
set UpgradeStatusPollInterval 5
set ExpectedReplicaUpgradeDuration 3600
set UserReplicaRestartWaitDuration 3600

+10 ud=UD1
+20 ud=UD2
+30 ud=UD3
verify

#
# Application version 1.0
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA stateful
app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA version=1.0
app.upload version10
provisionapp version10

#
# Application version 1.1
#
app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackageA version=1.1
app.servicetypes version11 ServicePackageA ServiceTypeA stateful
app.codepack version11 ServicePackageA CodeA1 types=ServiceTypeA version=1.1
app.upload version11
provisionapp version11

# Create application version 1.0
createapp fabric:/MyApp TestApp 1.0
verify

# Create services
createservice fabric:/MyApp/Volatile ServiceTypeA y 1 3 minreplicasetsize=2 appname=fabric:/MyApp
verify

addbehavior b1 * * AddReplicaReply

# Perform application upgrade from version 1.0 to 1.1
upgradeapp fabric:/MyApp 1.1 Rolling

!waitforstate FM.Replica.State.fabric:/MyApp/Volatile.10 InBuild
!waitforstate RA.Replica.State.fabric:/MyApp/Volatile.30.10 RD

!var PartitionId FM.FT.PartitionId.fabric:/MyApp/Volatile#0
!var ReplicaId RA.Replica.ReplicaId.fabric:/MyApp/Volatile.30.10

setcodepackagekillpending 10 TestApp_App0:ServicePackageA#CodeA1
clientreportfault 10 <var.PartitionId> <var.ReplicaId> permanent true

#!waitforstate FM.Replica.State.fabric:/MyApp/Volatile.10 Dropped

removebehavior b1

verifyupgradeapp fabric:/MyApp

verify

-*

!q
