#
# This test case covers the scenario where a notification-only upgrade is going
# on while a node is getting deactivated. Even if FM has already sent the
# message to node for the notification-only upgrade, we should still perform
# safety checks for node deactivation.
#

votes 10
cleantest
fmservice 1 1
cmservice 1 1
namingservice 1 1 1

set DummyPLBEnabled true
set ExpectedNodeDeactivationDuration 5
set HealthReportSendInterval 0
set HealthReportRetrySendInterval 1
set UpgradeStatusPollInterval 1

+10
verify

+20
+30
verify

#
# Application version 1.0
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA stateful persist
app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA version=1.0
app.upload version10
provisionapp version10

#
# Application version 1.1
#
app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackageA version=1.1
app.servicetypes version11 ServicePackageA ServiceTypeA stateful persist
app.codepack version11 ServicePackageA CodeA1 types=ServiceTypeA version=1.0
app.upload version11
provisionapp version11

# Create application version 1.0
createapp fabric:/MyApp TestApp 1.0
verify

# Create a service
createservice fabric:/MyApp/ServiceA ServiceTypeA y 1 3 persist appname=fabric:/MyApp minreplicasetsize=3 replicarestartwaitduration=3600
verify

# Bring down node 30 so that the partition is on the verge of quorum loss
-30
!waitforstate FM.Replica.IsUp.fabric:/MyApp/ServiceA.30 false

# Try to deactivate node 20 with intent Restart. Node deactivation should fail because of safety check.
deactivatenode 20 Restart
!waitforstate FM.Node.DeactivationIntent.20 Restart
!waitforstate FM.Node.DeactivationStatus.20 DeactivationSafetyCheckInProgress

# Block upgrade messages from FM to node 20
addbehavior b1 * 20 NodeUpgradeRequest

# Start a notification only upgrade
upgradeapp fabric:/MyApp 1.1 Rolling

# Node deactivation should still not complete
queryhealth node nodeid=20 expectedhealthstate=warning
!waitforstate FM.Node.DeactivationIntent.20 Restart
!waitforstate FM.Node.DeactivationStatus.20 DeactivationSafetyCheckInProgress

# Bring node 30 back up
+30
!waitforstate FM.Replica.IsUp.fabric:/MyApp/ServiceA.30 true

# Node deactivation should now complete
!waitforstate FM.Node.DeactivationIntent.20 Restart
!waitforstate FM.Node.DeactivationStatus.20 DeactivationComplete
queryhealth node nodeid=20 expectedhealthstate=ok

# Activate node 20
activatenode 20

# Unblock upgrade messages
removebehavior b1

# Ensure that the ugprade is complete
verifyupgradeapp fabric:/MyApp

verify

-*

!q
