votes 10 20 30
cleantest
fmservice 3 1
cmservice 3 1
namingservice 1 3 1

set NamingOperationRetryTimeout 30
set HealthOperationTimeout 5
set HealthReportSendInterval 0
set HealthReportRetrySendInterval 5

set ExpectedNodeDeactivationDuration 5

+10
+20
+30
verify

# Verify that all the nodes are activated by default
!waitforstate FM.Node.DeactivationIntent.10 None
!waitforstate FM.Node.DeactivationIntent.20 None
!waitforstate FM.Node.DeactivationIntent.30 None

##########################
# Health state should be ok to start
##########################
queryhealth node nodeid=10 expectedhealthstate=ok
queryhealth node nodeid=20 expectedhealthstate=ok
queryhealth node nodeid=30 expectedhealthstate=ok

# Activate node on already activated node should not change health state
activatenode 30
!waitforstate FM.Node.DeactivationIntent.30 None
!waitforstate FM.Node.DeactivationStatus.30 None
queryhealth node nodeid=30 expectedhealthstate=ok

##########################
# Deactivating node stuck in SafetyCheckComplete should report health
##########################
# Block node deactivation
addbehavior b1 * * NodeDeactivateRequest

# Deactivate node 10, should report health
deactivatenode 10 Restart
!waitforstate FM.Node.DeactivationIntent.10 Restart
!waitforstate FM.Node.DeactivationStatus.10 DeactivationSafetyCheckComplete

# Wait for some time, and check the node deactivation status and health state
!pause 2
!waitforstate FM.Node.DeactivationStatus.10 DeactivationSafetyCheckComplete
queryhealth node nodeid=10 expectedhealthstate=warning

# Unblock node deactivation, deactivate should complete and health should become ok
removebehavior b1
!waitforstate FM.Node.DeactivationStatus.10 DeactivationComplete

# Wait for some time, and check the node deactivation status and health state
!pause 2
!waitforstate FM.Node.DeactivationStatus.10 DeactivationComplete
queryhealth node nodeid=10 expectedhealthstate=ok

# Reactivate the node and check the health state remains ok
activatenode 10
!waitforstate FM.Node.DeactivationIntent.10 None
!waitforstate FM.Node.DeactivationStatus.10 None
queryhealth node nodeid=10 expectedhealthstate=ok

##########################
# Deactivating node stuck in SafetyCheckInProgress should report health
##########################
# Deactivate a node and cause it to get stuck during SafetyCheckInProgress
+40
+50
verify

# Create services
createservice fabric:/stateless CalculatorServiceType n 1 -1
createservice fabric:/persisted TestPersistedStoreServiceType y 1 5 persist minreplicasetsize=5
verify

# Bring down nodes 40 and 50 so that fabric:/persisted is at quorum
-40
-50
!waitforstate FM.Node.IsUp.40 false
!waitforstate FM.Node.IsUp.50 false

# Deactivate node 30 with intent Restart
deactivatenode 30 Restart
!waitforstate FM.Node.DeactivationIntent.30 Restart
!waitforstate FM.Node.DeactivationStatus.30 DeactivationSafetyCheckInProgress

# Wait for some time, and check the node deactivation status and health state
!pause 2
!waitforstate FM.Node.DeactivationStatus.30 DeactivationSafetyCheckInProgress
queryhealth node nodeid=30 expectedhealthstate=warning

# Bring up nodes 40 and 50 and allow the node deactivation to complete and health to return to ok
+40
+50
!waitforstate FM.Node.DeactivationStatus.30 DeactivationComplete

# Wait for some time, and check the node deactivation status and health state
!pause 2
!waitforstate FM.Node.DeactivationStatus.30 DeactivationComplete
queryhealth node nodeid=30 expectedhealthstate=ok

# Reactivate the node and check the health state remains ok
activatenode 30
!waitforstate FM.Node.DeactivationIntent.30 None
!waitforstate FM.Node.DeactivationStatus.30 None
queryhealth node nodeid=30 expectedhealthstate=ok

verify

!q
