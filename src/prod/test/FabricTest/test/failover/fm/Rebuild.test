#
# [owner] MMohsin, AprameyR
#
# This test case covers the scenario where a replica is lost after rebuild,
# and the node comes up and reports the replica it had. If we don't add the
# replica back into the replica set, we can end up in a situation where if
# we attempt to place another replica on that node, and another rebuild happens,
# then we can run into an assert because of ReplicaId and NodeInstance
# validation failure between the old and new replica.
#

votes 10 20 30
cleantest
fmservice 1 1
cmservice 3 2
namingservice 1 3 2

set DummyPLBEnabled true
set FMPlacementConstraints FM==true
set CMPlacementConstraints System==true
set NamingPlacementConstraints System==true

+10 nodeprops=FM:true,System:false,User:false
+20 nodeprops=FM:false,System:true,User:false
+30 nodeprops=FM:false,System:true,User:true
+40 nodeprops=FM:false,System:true,User:true
+50 nodeprops=FM:false,System:false,User:true
verify

# Create a persisted service
createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist constraint=(User==true)
verify

# Bring down node 50, so that it is not discovered in a subsequent rebuild
-50
!waitforstate FM.Replica.IsUp.fabric:/persisted.50 false
!waitforstate FM.FT.Reconfig.fabric:/persisted false

# Perform rebuild
-10
!waitforstate FM.Replica.IsUp.FMService.30 NullFT
recoverpartition 00000000-0000-0000-0000-000000000001
+10 nodeprops=FM:true,System:false,User:false
!waitforstate FM.Replica.IsUp.FMService.10 true
!waitforstate FM.IsReady true

addbehavior b1 * * CreateReplica
addbehavior b2 * * ReplicaUpReply
addbehavior b3 * * RemoveReplica

# Bring node 50 back up, so that it can report the old replica it had.
# If we ignore the incoming replica, then PLB will be trying to place
# another replica on node 50.
+50 nodeprops=FM:false,System:false,User:true
!waitforstate RA.Replica.IsUp.fabric:/persisted.40.50 true

# Bring down node 50
-50
!waitforstate FM.Node.IsUp.50 false

# Perform another rebulid and bring up node 50 during the rebuild
-10
!waitforstate FM.Replica.IsUp.FMService.30 NullFT
+50 nodeprops=FM:false,System:false,User:true
!wait
recoverpartition 00000000-0000-0000-0000-000000000001
+10 nodeprops=FM:true,System:false,User:false
!waitforstate FM.Replica.IsUp.FMService.10 true
!waitforstate FM.IsReady true

removebehavior b3
removebehavior b2
removebehavior b1

verify

-*

!q
