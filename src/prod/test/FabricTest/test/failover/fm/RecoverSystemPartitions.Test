set DummyPLBEnabled true
votes 10
cleantest
fmservice 3 1
cmservice 0 0
namingservice 1 3 1

set ExpectedClusterSize 5

+10
+20
+30
+40
+50
verify

# Verify the location of FMService replicas
!waitforstate FM.Replica.Role.FMService.50 Primary
!waitforstate FM.Replica.Role.FMService.40 Secondary
!waitforstate FM.Replica.Role.FMService.30 Secondary

# Verify the location of NamingService replicas
!waitforstate FM.Replica.Role.NamingService.50 Primary
!waitforstate FM.Replica.Role.NamingService.40 Secondary
!waitforstate FM.Replica.Role.NamingService.30 Secondary

# Create a persisted stateful service
createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist
verify

# Verify the location of user service replicas
!waitforstate FM.Replica.Role.fabric:/persisted.50 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.40 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.30 Secondary

# Bring all the services into quorum loss
addbehavior b1 * * GetLSN
-50
-40
!waitforstate FM.FT.QuorumLost.FMService true
removebehavior b1

# Recover the FMService. This will cause data loss and trigger full rebuild.
# The NamingService will then be in quorum loss, which should also get recovered
# after an internal retry.
recoversystempartitions

# Verify that the FMService and the NamingService are no longer in quorum loss
!waitforstate FM.FT.QuorumLost.FMService false
!waitforstate FM.FT.QuorumLost.NamingService false

# The user service should still be in quorum loss
!waitforstate FM.FT.QuorumLost.fabric:/persisted true

# Recover the user service
recoverservicepartitions fabric:/persisted

# Verify that the user service is no longer in quorum loss state
!waitforstate FM.FT.QuorumLost.fabric:/persisted false

verify

# Verify the location of FMService replicas
!waitforstate FM.Replica.Role.FMService.30 Primary
!waitforstate FM.Replica.Role.FMService.20 Secondary
!waitforstate FM.Replica.Role.FMService.10 Secondary

# Verify the location of NamingService replicas
!waitforstate FM.Replica.Role.NamingService.30 Primary
!waitforstate FM.Replica.Role.NamingService.20 Secondary
!waitforstate FM.Replica.Role.NamingService.10 Secondary

# Verify the location of user service replicas
!waitforstate FM.Replica.Role.fabric:/persisted.30 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.20 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.10 Secondary

addbehavior b1 * * ChangeNotification
-30
-20
-10
removebehavior b1

!pause,5
clearticket

set ExpectedClusterSize 1

+10
!wait

# All the services should be in quorum loss. Recover the system services.
recoversystempartitions

# Verify that the FMService and the NamingService are no longer in quorum loss
!waitforstate FM.FT.QuorumLost.FMService false
!waitforstate FM.FT.QuorumLost.NamingService false

# The user service should still be in quorum loss. Recover it.
recoverservicepartitions fabric:/persisted

# Verify that the user service is no longer in quorum loss state
!waitforstate FM.FT.QuorumLost.fabric:/persisted false

verify

-*

!q
