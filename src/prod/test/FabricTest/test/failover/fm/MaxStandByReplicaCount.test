#
# [owner] MMohsin
#
# This test case validates various scenarios that involve extra StandBy replicas. If there are more than
# MaxStandByReplicaCount of StandBy replicas, additional StandBy replicas should only be dropped if the
# partition is in a stable state.
#

votes 10 20 30
fmservice 3 2
cmservice 3 2
namingservice 1 3 2
cleantest

set DummyPLBEnabled true
set DeletedReplicaKeepDuration 3

+10
+20
+30
verify

+40
+50
verify

createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist replicarestartwaitduration=5
verify

################################################################################
# Scenario 1: Two StandBy replicas when the partition is stable
################################################################################

-50
verify

-40
verify

set MaxActionsPerIteration 0

+40
!waitforstate FM.Replica.State.fabric:/persisted.40 StandBy

+50
!waitforstate FM.Replica.State.fabric:/persisted.50 StandBy

set MaxActionsPerIteration 500

!waitforstate FM.Replica.State.fabric:/persisted.40 Dropped
!waitforstate FM.FT.ReplicaCount.fabric:/persisted 4

verify

################################################################################
# Scenario 2: Two StandBy replicas when the partition is not stable
################################################################################

+60
verify

movesecondary fabric:/persisted 10 40
!waitforstate FM.Replica.Role.fabric:/persisted.40 Secondary
verify

updateservice fabric:/persisted Stateful ReplicaRestartWaitDuration=600
!waitforstate FM.Service.ReplicaRestartWaitDuration.fabric:/persisted 600

-40
!waitforstate FM.Replica.IsUp.fabric:/persisted.40 false
!waitforstate FM.Replica.Role.fabric:/persisted.40 Idle
!waitforstate FM.FT.Reconfig.fabric:/persisted false

-30
!waitforstate FM.Replica.IsUp.fabric:/persisted.30 false
!waitforstate FM.Replica.Role.fabric:/persisted.30 Idle
!waitforstate FM.FT.Reconfig.fabric:/persisted false

set MaxActionsPerIteration 0

+30
!waitforstate FM.Replica.State.fabric:/persisted.30 StandBy

+40
!waitforstate FM.Replica.State.fabric:/persisted.40 StandBy

set MaxActionsPerIteration 500

!waitforstate FM.Replica.State.fabric:/persisted.20 Ready
!waitforstate FM.Replica.State.fabric:/persisted.30 StandBy
!waitforstate FM.Replica.State.fabric:/persisted.40 Ready
!waitforstate FM.Replica.State.fabric:/persisted.50 Ready
!waitforstate FM.FT.ReplicaCount.fabric:/persisted 4

verify

-*

!q

