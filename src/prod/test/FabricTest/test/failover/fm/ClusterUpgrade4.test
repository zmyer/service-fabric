#
# [owner] MMohsin, AnuragG
#
# This test covers the scenario where a volatile service has a TargetReplicaSetSize of 1.
# During fabric upgrade, the primary should be proactively moved out of the node,
# and moved back in after the upgrade is complete on that node.
#

votes 10 20 30
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set DummyPLBEnabled true
set UpgradeStatusPollInterval 5
set NamingOperationTimeout 30
set NamingOperationRetryTimeout 150
set ExpectedNodeFabricUpgradeDuration 3600
set IsSingletonReplicaMoveAllowedDuringUpgrade true

+10 ud=UD3
+20 ud=UD2
+30 ud=UD1
verify

# Create services
createservice fabric:/volatile TestStoreServiceType y 1 1
createservice fabric:/persisted TestPersistedStoreServiceType y 1 1 persist
verify

!waitforstate FM.Replica.Role.fabric:/volatile.30 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.30 Primary

# Block the NodeFabricUpgradeRequest so that node 30 does not start the upgrade
addbehavior b1 * * NodeFabricUpgradeRequest

#
# Perform fabric upgrade from version 1.0 to 1.1
#
prepareupgradefabric code=4.2.765.0 config=version5
provisionfabric code=4.2.765.0 config=version5
upgradefabric code=4.2.765.0 config=version5 Rolling

# The primary replica should be moved out of node 30
!waitforstate FM.Replica.Role.fabric:/volatile.20 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.20 Primary

# Allow the upgrade to proceed
removebehavior b1

verifyupgradefabric

# The primary replica should have been moved back to node 30
!waitforstate FM.Replica.Role.fabric:/volatile.30 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.30 Primary

verify

-*

!q
