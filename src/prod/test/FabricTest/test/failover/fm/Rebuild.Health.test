votes 10 20 30
cleantest

fmservice 1 1
cmservice 3 2
namingservice 1 3 2

set DummyPLBEnabled true
set ExpectedClusterSize 3
set FMPlacementConstraints system==true
set NamingPlacementConstraints system==true
set CMPlacementConstraints system==true
set HealthOperationTimeout 5
set HealthReportSendInterval 0
set HealthReportRetrySendInterval 5
set RebuildPartitionTimeLimit 5
set HealthStoreCleanupInterval 1
set HealthStoreCleanupGraceInterval 2

+10 nodeprops=system:true
+20 nodeprops=system:true
+30 nodeprops=system:true
verify

!waitforstate FM.Replica.Role.FMService.30 Primary

+40 nodeprops=system:false
+50 nodeprops=system:false
+60 nodeprops=system:false
verify

createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist replicarestartwaitduration=3600
verify

!waitforstate FM.Replica.Role.fabric:/persisted.40 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.50 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.60 Primary

# Verfiy that health has been successfully reported for fabric:/persisted
queryhealth service servicename=fabric:/persisted expectedhealthstate=ok

!var FMServiceFTID FM.FT.PartitionId.FMService
!var UserServiceFTID FM.FT.PartitionId.fabric:/persisted

################################################################################
# Scenario 1: Health is reported for partition stuck in rebuild
################################################################################

# Bring down all replicas of fabric:/persisted
-40
-50
-60
!waitforstate FM.Replica.IsUp.fabric:/persisted.40 false
!waitforstate FM.Replica.IsUp.fabric:/persisted.50 false
!waitforstate FM.Replica.IsUp.fabric:/persisted.60 false
!waitforstate FM.FT.QuorumLost.fabric:/persisted true

# Bring FM service into quourm loss
-30
!waitforstate FM.FT.QuorumLost.FMService true

# Bring up node 40
+40 nodeprops=system:false
!wait
!waitforstate FMM.Node.40 true

# Trigger rebuild
recoverpartition <var.FMServiceFTID>

# fabric:/persisted should stuck in rebuild
!waitforstate FM.FT.IsInRebuild.fabric:/persisted true

# Verify that health is reported for fabric:/persisted
queryhealth partition partitionguid=<var.UserServiceFTID> expectedhealthstate=warning

################################################################################
# Scenario 2: Health should go back to OK after partition rebuild is complete
################################################################################

# Bring the nodes up to complete the rebuild of the partition
+30 nodeprops=system:true
+50 nodeprops=system:false
+60 nodeprops=system:false
verify

# Verify that health is reported for fabric:/persisted
queryhealth partition partitionguid=<var.UserServiceFTID> expectedhealthstate=OK

################################################################################
# Scenario 3: Health should be deleted if an inbuild partition is deleted
################################################################################


# Bring down all replicas of fabric:/persisted
-40
!waitforstate FM.Replica.IsUp.fabric:/persisted.40 false
-50
!waitforstate FM.Replica.IsUp.fabric:/persisted.50 false
-60
!waitforstate FM.Replica.IsUp.fabric:/persisted.60 false

!waitforstate FM.FT.QuorumLost.fabric:/persisted true

# Bring FM service into quourm loss
-20
!waitforstate FM.FT.QuorumLost.FMService true

# Bring up node 40
+40 nodeprops=system:false
!wait
!waitforstate FMM.Node.40 true

# Trigger rebuild
recoverpartition <var.FMServiceFTID>

# fabric:/persisted should stuck in rebuild
!waitforstate FM.FT.IsInRebuild.fabric:/persisted true

# Verify that health is reported for fabric:/persisted
queryhealth partition partitionguid=<var.UserServiceFTID> expectedhealthstate=warning

set InBuildFailoverUnitKeepDuration 5

# The inbuild partition and its corresponding health entry should have been deleted
!waitforstate FM.FT.IsInRebuild.fabric:/persisted false
queryhealth partition partitionguid=<var.UserServiceFTID> expectempty=true

# Wait for the health entity to get cleaned up at the HM
!pause 5

set InBuildFailoverUnitKeepDuration 3600

# Bring the nodes up
+20 nodeprops=system:true
+50 nodeprops=system:false
+60 nodeprops=system:false
verify

# The partition should have been created again and should be healthy
queryhealth partition partitionguid=<var.UserServiceFTID> expectedhealthstate=OK


!q
