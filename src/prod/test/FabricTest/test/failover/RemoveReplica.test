#
# [owner] aprameyr
# Associated bug#:
# 
set DummyPLBEnabled true

votes 10 
cmservice 0 0
fmservice 1 1
namingservice 1 1 1
cleantest

+10 nodeprops=StringProperty:SeedNode
verify

#################
# Scenario 1: Simple Add Replica followed by remove replica of same id
################

addbehavior b1 * * ReplicatorBuildIdleReplicaReply

+20 nodeprops=StringProperty:Normal
+22 nodeprops=StringProperty:Normal
verify

createservice fabric:/test1 TestStoreServiceType y 1 2 constraint=(StringProperty!=SeedNode)

!waitforstate RA.Replica.State.fabric:/test1.22.20 IB
!waitforstate FM.Replica.State.fabric:/test1.20 InBuild

-20 removedata
!waitforstate FM.Node.IsUp.20 false

!waitforstate RA.Replica.Exists.fabric:/test1.22.20 false

deleteservice fabric:/test1

-22 removedata
!wait

nodestateremoved 20
nodestateremoved 22

removebehavior b1
verify

##########################
# Scenario 2: Add Replica and then remove replica gets lost and FM adds a replica with another instance id
#########################

addbehavior b2 * * ReplicatorBuildIdleReplicaReply
addbehavior b3 * * RemoveReplica

#block replica down because the replica down processing can cause 
#the test to fail because FM adds a replica with higher id
addbehavior bxx * * ReplicaUp

+30 nodeprops=StringProperty:Normal
+32 nodeprops=StringProperty:Normal
verify

createservice fabric:/test2 TestStoreServiceType y 1 2 constraint=(StringProperty!=SeedNode)

!waitforstate RA.Replica.State.fabric:/test2.32.30 IB
!waitforstate FM.Replica.State.fabric:/test2.30 InBuild

-30
!waitforstate FM.Node.IsUp.30 false
!waitforstate FM.Replica.IsUp.fabric:/test2.30 false

#the RemoveReplica is blocked hence the replica should still be on the primary
!waitforstate RA.Replica.State.fabric:/test2.32.30 IB
!waitforstate RA.Replica.IsUp.fabric:/test2.32.30 true

removebehavior bxx

+30 nodeprops=StringProperty:Normal
!waitforstate FM.Node.IsUp.30 true

!waitforstate FM.Replica.IsUp.fabric:/test2.30 true
!waitforstate FM.Replica.State.fabric:/test2.30 InBuild

#now unblock build reply 
#the addreplica for the new replica on 30 should have removed the idle from the replicator
#and then added a new idle 
#which should cause reconfig to complete
removebehavior b2

verify

deleteservice fabric:/test2
-32 removedata
-30 removedata

nodestateremoved 32
nodestateremoved 30
removebehavior b3
verify

#######################################
# Scenario 3: Remove Replica for a different replica id
#######################################

addbehavior b10 42 40 CopyOperation
addbehavior b4 * * ReplicatorBuildIdleReplicaReply
addbehavior b5 * * RemoveReplica

+40 nodeprops=StringProperty:Normal
+42 nodeprops=StringProperty:Normal
verify

createservice fabric:/test4 TestStoreServiceType y 1 2 constraint=(StringProperty!=SeedNode)

!waitforstate RA.Replica.State.fabric:/test4.42.40 IB
!waitforstate FM.Replica.State.fabric:/test4.40 InBuild

# block AddReplica for the new replica
addbehavior b6 * * AddReplica

-40
!waitforstate FM.Node.IsUp.40 false
!waitforstate FM.Replica.IsUp.fabric:/test4.40 false


#the RemoveReplica is blocked hence the replica should still be on the primary
!waitforstate RA.Replica.State.fabric:/test4.42.40 IB
!waitforstate RA.Replica.IsUp.fabric:/test4.42.40 true

+40 nodeprops=StringProperty:Normal
!waitforstate FM.Node.IsUp.40 true

!waitforstate FM.Replica.IsUp.fabric:/test4.40 true
!waitforstate FM.Replica.State.fabric:/test4.40 InBuild

!pause 10

#at this point FM is trying to add a new idle replica with id r2
#Ra has the old replica with id R1
#on taking down the node the FM and RA should have a mismatch of states
-40
!waitforstate FM.Node.IsUp.40 false

#remove all the blocks
removebehavior b6
removebehavior b5
removebehavior b4

#at this point everybody should be in sync 
#add anohter node and see that everything completes

addbehavior b8 42 41 ReplicationOperation

+41 nodeprops=StringProperty:Normal
!waitforstate FM.Node.IsUp.41 true
verify

clientput fabric:/test4 10 10da
!wait

#perform swap primary (aka catchup all) and validate everything works
swapprimary fabric:/test4 42 41

!waitforstate RA.FT.ReconfigStage.fabric:/test4.42 Phase2_Catchup
removebehavior b8


verify
!q
